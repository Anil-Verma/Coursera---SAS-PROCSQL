*creating the libname sq****;
libname sq '/home/PATH/ESQ1M6/data';

** SELECTING ONLY 1 STATE **;
PROC SQL OUTOBS=20;
	SELECT CUSTOMERID, CITY, STATE
	FROM SQ.CUSTOMER 
	WHERE STATE='NC';
QUIT;

PROC SQL OUTOBS=40;
	SELECT CUSTOMERID, CITY, STATE
	FROM SQ.CUSTOMER 
	WHERE STATE IN ('NY','NC', 'CA');
QUIT;


proc sql  number;
select FirstName, LastName, State 
   from sq.customer
   where State IN ('VT', 'SC', 'GA');
quit;


** LOGICAL OPERATOR	***;
PROC SQL OUTOBS=40;
	SELECT CUSTOMERID, DOB
	FROM SQ.CUSTOMER 
	WHERE INCOME > 30000 AND
			STATE = 'NC';
QUIT;


/*********		MISSING VALUES			*******/
PROC SQL NUMBER;
	SELECT FIRSTNAME, LASTNAME, USERID, CREDITSCORE
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE IS NOT NULL 
			AND CREDITSCORE <500
			;
QUIT;



/***** where SPECIAL operator		******/
PROC SQL OUTOBS=10 NUMBER;
	SELECT USERID, FIRSTNAME, CREDITSCORE
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE BETWEEN 700 AND 799;
QUIT;


PROC SQL OUTOBS=20 NUMBER;
	SELECT USERID, FIRSTNAME
	FROM SQ.CUSTOMER
	WHERE FIRSTNAME LIKE 'A%';
QUIT;


PROC SQL NUMBER;
	SELECT USERID, FIRSTNAME
	FROM SQ.CUSTOMER
	WHERE FIRSTNAME LIKE 'Z_l%';
QUIT;


** ORDER BY**;
PROC SQL OUTOBS=10 NUMBER;
	SELECT LASTNAME, CREDITSCORE
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE IS NOT NULL
	ORDER BY CREDITSCORE;
QUIT;

PROC SQL OUTOBS=10 NUMBER;
	SELECT FIRSTNAME, LASTNAME, CREDITSCORE
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE >830
	ORDER BY CREDITSCORE DESC, LASTNAME;
QUIT;

PROC SQL OUTOBS=10 NUMBER;
	SELECT FIRSTNAME, CREDITSCORE
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE >830
	ORDER BY CREDITSCORE DESC, LASTNAME;
QUIT;


/***********************		ENHANCING REPORTS			************/
 /*
 	title1 "title";
 	footnote1 "footnote";
 	
 	title1;
 	footnote1;
 */

TITLE1 "CUSTOMER DEMOGRAPHIC REPORT";
FOOTNOTE1 "12TH NOVEMBER 2020";

PROC SQL OUTOBS=40 NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE,
			USERID LABEL="EMAIL ADDRESS",
			INCOME LABEL="ESTIMATED INCOME" FORMAT=DOLLAR16.2,
			DOB FORMAT=DATE9.
	FROM SQ.CUSTOMER;
QUIT;


** CLEARING THE TITLE AND FOOTNOTES;
TITLE;
FOOTNOTE;


**	CHANGING THE FORMAT TO OBSERVE CHANGES	***;
TITLE1 "Customers from Hawaii";
FOOTNOTE1 "12TH NOVEMBER 2020";

PROC SQL NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE,
			USERID LABEL="EMAIL ADDRESS",
			INCOME LABEL='ESTIMATED INCOME' FORMAT=DOLLAR16.2,
			DOB FORMAT=DATE9.
	FROM SQ.CUSTOMER
	WHERE STATE="HI" AND
          BANKID IS NOT NULL
	ORDER BY INCOME DESC;
quit;

TITLE;
FOOTNOTE;


TITLE1 "Customers from Hawaii";
FOOTNOTE1 "12TH NOVEMBER 2020";

PROC SQL NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE,
			USERID LABEL="EMAIL ADDRESS",
			INCOME LABEL='ESTIMATED INCOME' FORMAT=DOLLAR7.2,
			DOB FORMAT=DATE9.
	FROM SQ.CUSTOMER
	WHERE STATE="HI" AND
          BANKID IS NOT NULL
	ORDER BY INCOME DESC;
quit;

TITLE;
FOOTNOTE;


/*************   REPORTS		*************/
/*	REPORT1
	NO BANK ID
	CREDIT SCORE > 700
	TOP 10 CUSTOMERS BY INCOME
*/

TITLE1 "TOP 10 CUSTOMERS BY INCOME WITHOUT A BANK ID AND CREDITSCORE OVER 700";
TITLE2 "MARKETING REPORT";
FOOTNOTE1 "12TH NOVEMBER 2020";

PROC SQL OUTOBS=10 NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE, BANKID, CREDITSCORE,
			INCOME LABEL="ESTIMATED INCOME" FORMAT=DOLLAR12.2,
			USERID LABEL="EMAIL ADDRESS"
	FROM SQ.CUSTOMER
	WHERE CREDITSCORE > 700 AND
			BANKID IS NULL
	ORDER BY INCOME DESC;
QUIT;

TITLE;
FOOTNOTE;


/*	REPORT2
	BORN PRIOR TO DECEMBER 30, 1940
	EMPLOYED
*/

TITLE1 "DOB PRIOR TO DECEMBER 30, 1940";
TITLE2 "RETIREMENT CAMPAIGN";

PROC SQL;
	SELECT CUSTOMERID, STATE, 
			ZIP FORMAT=Z5.,
			DOB FORMAT=DATE9.,
			USERID LABEL="EMAIL",
			HOMEPHONE, CELLPHONE
	FROM SQ.CUSTOMER
	WHERE DOB < "30DEC1940"D
			AND EMPLOYED='Y'
	ORDER BY DOB DESC;
QUIT;

TITLE;
FOOTNOTE;
	

/*** 	CREATING A NEW COLUMN FOR CALCULATION		***/
TITLE1 "PEOPLE WHO ARE ABOVE 70 YEARS OF AGE";

PROC SQL OUTOBS=20 NUMBER;
	SELECT FIRSTNAME, LASTNAME,
			USERID LABEL="EMAIL",
			YRDIF(DOB, '12NOV2020'D) AS AGE FORMAT=3.
	FROM SQ.CUSTOMER
	ORDER BY AGE DESC;
QUIT;

TITLE;


**		USING CALCULATED COLUMN		**;
PROC SQL;
	SELECT FIRSTNAME, LASTNAME, 
			USERID LABEL="EMAIL",
			YRDIF(DOB, '01JAN2019'D) AS AGE
	FROM SQ.CUSTOMER
	WHERE AGE >= 70;
QUIT;

PROC SQL OUTOBS=20 NUMBER;
	SELECT FIRSTNAME, LASTNAME, 
			USERID LABEL="EMAIL",
			YRDIF(DOB, '01JAN2019'D) AS AGE
	FROM SQ.CUSTOMER
	WHERE CALCULATED AGE >= 70
	ORDER BY AGE DESC;
QUIT;


/***********		CASE EXPRESSION			****/
PROC SQL OUTOBS=40 NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE, CREDITSCORE, 
		CASE
			WHEN CREDITSCORE >= 750 THEN "EXCELLENT"
			WHEN CREDITSCORE >= 700 THEN "GOOD"
			WHEN CREDITSCORE >= 650 THEN "FAIR"
			WHEN CREDITSCORE >= 550 THEN "POOR"
			WHEN CREDITSCORE >= 0 	THEN "BAD"
			ELSE "UNKNOWN"
		END AS CATEGORY
	FROM SQ.CUSTOMER(OBS=100);
QUIT;


PROC SQL OUTOBS=40 NUMBER;
	SELECT FIRSTNAME, LASTNAME, STATE, CREDITSCORE,
			CASE MARRIED
				WHEN "M" THEN "MARRIED"
				WHEN "D" THEN "DIVORCED"
				WHEN "S" THEN "SINGLE"
				WHEN "W" THEN "WIDOWED"
				ELSE "UNKNOWN"
			END AS MARITAL_STATUS
	FROM SQ.CUSTOMER(OBS=100);
QUIT;


/**********				QUERYING A TABLE			********/
TITLE1 "Large Non-Educational Transactions";

PROC SQL OUTOBS=10 NUMBER;
	SELECT CUSTOMERNAME LABEL="CUSTOMER NAME", 
			MERCHANTNAME,
			TYPE,
			SERVICE,
			AMOUNT FORMAT=DOLLAR10.2 LABEL="TRANSACTION AMOUNT"
	FROM SQ.TRANSACTIONFULL
	WHERE AMOUNT > 1000 AND
			SERVICE NE 'University'
	ORDER BY AMOUNT DESC;
QUIT;
			
TITLE;


*** QUERY 2***;
TITLE1 "November/December Transactions";

PROC SQL NUMBER;
	SELECT CUSTOMERNAME LABEL="CUSTOMER NAME",
			MERCHANTNAME LABEL="MERCHANT NAME",
			AMOUNT FORMAT=DOLLAR9.2 LABEL="TRANSACTION AMOUNT",
			DATEPART(DATETIME) AS TRANSACTIONDATE FORMAT=DATE9. LABEL="TRANSACTION DATE"
	FROM SQ.TRANSACTIONFULL
	WHERE SERVICE NE "University" AND
			MONTH(CALCULATED TRANSACTIONDATE) IN (12)
	ORDER BY DATETIME;
QUIT;

TITLE;
	

